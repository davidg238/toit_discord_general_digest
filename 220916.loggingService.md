## 2022-09-16

https://discordapp.com/channels/918498540232253480/918498540232253483/1020286528401965086

### Rikke
Hello again  We are trying to implement a logging service, but we encountered a problem. The logging service doesn't start properly, and only few parts of the code logged correctly. We have pinned down the problem, and found out the vm didn't properly start the service unless it encountered a sleep, or the logging service was run in different services.   
We made a little example:   
```
import system.services
import system.api.log show *
import log

class LoggingServiceDefinition extends services.ServiceDefinition implements LogService:

  constructor:
    super "example/logging" --major=1 --minor=0
    provides LogService.UUID LogService.MAJOR LogService.MINOR

  handle pid/int client/int index/int arguments/any -> any:
    if index == LogService.LOG_INDEX:
      return log arguments[0] arguments[1] arguments[2] arguments[3] arguments[4]
    unreachable

  log level/int message/any names/List? keys/List? values/List? -> none:
    print "mylog $level:$message"

run_server:
  logging := LoggingServiceDefinition
  logging.install
  logging.uninstall --wait

main:
  spawn::
    run_server

  sleep --ms=10
  10.repeat:
    log.info "hello"
    sleep --ms=100
```
This code will run the correct logger, but if the sleep --ms=10 is removed, the logging service will not be started, and it will log the hello's with the default logger.  
Is it possible to run the service putting a sleep?  

### kasperl
The problem is that you end up resolving the logging service a bit too early. You can wait for it (not based on time), if you create a LogServiceClient before the loop.  
```
run_server:
  logging := LoggingServiceDefinition
  logging.install
  logging.uninstall --wait

main:
  spawn::
    run_server

  LogServiceClient  // Wait for the logging service to get started.
  10.repeat:
    log.info "hello"
    sleep --ms=100
```

I'd probably also drop the `logging.uninstall --wait` part unless you really want it to stop after the first client goes away.  
Maybe we need to fix log so it waits a bit before it insists on resolving the logging interface.  

### Rikke
LogServiceClient works thanks, and yes I need to remove the `logging.uninstall --wait` aswell   

### kasperl
You don't have to run the server in a separate process if you don't want to. That is another fix.  
```
main:
  logging := LoggingServiceDefinition
  logging.install
  10.repeat:
    log.info "hello"
    sleep --ms=100
```
(so the code that uses the logging client is guaranteed to run after the service was installed)  

