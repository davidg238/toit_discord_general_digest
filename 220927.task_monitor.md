## 2022-09-27

https://discordapp.com/channels/918498540232253480/1024249534051790848/1024273648296677387

#### kasperl
This is nothing more than a few minutes of hacking, but let me know if I'm on the right track, @Rikke:  
```
import monitor

main:
  watchdog := Watchdog
  watchdog.add "hans":: | notifier/WatchdogNotifier |
    while true:
      if (random 100) < 20:
        notifier.ping
      sleep --ms=1_000
  watchdog.add "finn":: | notifier/WatchdogNotifier |
    while true:
      if (random 100) < 5:
        notifier.ping
      sleep --ms=100
  watchdog.run

interface WatchdogNotifier:
  ping -> none

class Watchdog:
  tasks_ := []

  add name/string code/Lambda -> none
      --period/Duration=(Duration --s=5):
    tasks_.add (WatchdogTask_ name code period)

  run -> none:
    tasks_.do: it.start
    while true:
      // Restart tasks that are late.
      now := Time.monotonic_us
      tasks_.do:
        if now > it.next: it.restart
      // Sleep until it is possible for a task to be late.
      now = Time.monotonic_us
      wait_until := null
      tasks_.do:
        if not wait_until: wait_until = it.next
        else: wait_until = min wait_until it.next
      wait_for := wait_until - now
      sleep (Duration --us=wait_for)

class WatchdogTask_ implements WatchdogNotifier:
  name/string
  code_/Lambda
  period_us_/int

  task_ := null
  last_ping_ := null
  done_ := null

  constructor .name .code_ period/Duration:
    period_us_ = period.in_us

  next -> int:
    last := last_ping_
    return last + period_us_

  ping -> none:
    last_ping_ = Time.monotonic_us

  restart -> none:
    print "$(Time.monotonic_us): restarting $name"
    if task_: task_.cancel
    // handling timeouts here is a bit tricky
    catch:
      with_timeout --ms=1_000: done_.get
      done_ = null
      start

  start -> none:
    if task_: throw "ugh"
    ping
    done_ = monitor.Latch
    task_ = task::
      try:
        code_.call this
      finally:
        task_ = null
        critical_do: done_.set null
```
This is not production code at all, but it was kinda fun to put together.  
One task monitors two others and restarts them if they fail to ping.  
  
#### Rikke
Yup this was what i was looking for, you make it look simple and ping is used to reset the timer?  
  
#### kasperl  
Yeah. It probably needs better names -- and there is something iffy with what to do if we cannot shut down a task that isn't responding.  
(handling timeouts here is a bit tricky)  
  
#### floitsch
I guess there can be two different kinds of watchdogs: inner-app ones, watching tasks, and "launcher" watchdogs that ensure that full applications are alive.
In theory, a task-watchdog should be enough (as you can always run your app with a main: watch: program (obviously a bit nicer and more parametrized), but a launcher watchdog feels a bit more robust although I wouldn't be able to say why.  
  
#### Rikke
Yes task watchdog is fine for me, but a full watchdog for applications could be useful for others  
